# Maintainer: wenLiangcan <boxeed at gmail dot com>

pkgname=('python-wcwidth' 'python2-wcwidth')
pkgver=0.1.2
pkgrel=1
pkgdesc="Measures number of Terminal column cells of wide-character codes"
url="https://github.com/jquast/wcwidth"
license=('MIT')
arch=('any')
source=("https://pypi.python.org/packages/source/w/wcwidth/wcwidth-${pkgver}.tar.gz")
sha512sums=('9ea9c1485202106282b4d82b6a8d64c978faa47b7c8142f36db7bd997af4e27910b6fc41c9021c9293d27d38692eb55dc4c19b5b537a4dc545111fb48e1f4665')

prepare() {
    cp -a "${srcdir}/wcwidth-${pkgver}"{,-python2}
}

package_python-wcwidth() {
    depends=('python')
    makedepends=('python-setuptools')
    cd "wcwidth-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-wcwidth() {
    depends=('python2')
    makedepends=('python2-setuptools')
    cd "wcwidth-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1
}
