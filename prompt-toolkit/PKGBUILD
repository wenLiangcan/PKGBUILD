_module="prompt_toolkit"
pkgname=("python-${_module}" "python2-${_module}")
pkgver=0.21
pkgrel=1
pkgdesc="Library for building powerful interactive command lines in Python"
url="https://github.com/jonathanslenders/python-prompt-toolkit"
license=('CUSTOM')
arch=('any')
source=("https://pypi.python.org/packages/source/p/prompt_toolkit/prompt_toolkit-${pkgver}.tar.gz")
sha512sums=('1ea4df7005d97287f3d563957dd4e3ddd2f11b24cb29027eaa58344998e67f2760304e5de5eddffed8ccaddc36de0253834a60c5435dcc94b815e7464b739818')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

package_python-prompt_toolkit() {
    depends=('python' 'python-docopt' 'python-jedi' 'python-pygments' 'python-six' 'python-wcwidth')
    makedepends=('python-setuptools')
    optdepends=('ipython: ptipython (Prompt Toolkit IPython wrapper)')
    cd "$srcdir/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
}


package_python2-prompt_toolkit() {
    depends=('python2' 'python2-docopt' 'python2-jedi' 'python2-pygments' 'python2-six' 'python2-wcwidth')
    makedepends=('python2-setuptools')
    optdepends=('ipython2: ptipython (Prompt Toolkit IPython wrapper)')
    cd "$srcdir/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1
    mv "${pkgdir}/usr/bin/ptpython"{,2}
    mv "${pkgdir}/usr/bin/ptipython"{,2}
}
