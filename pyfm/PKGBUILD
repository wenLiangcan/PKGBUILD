# Maintainer: wenLiangcan <boxeed at gmail dot com>

pkgname=('pyfm' 'python2-pyfm')
pkgver=0.2.3
pkgrel=1
pkgdesc="A Tiny and Smart Terminal Player of douban.fm."
url="https://github.com/skyline75489/pyfm"
depends=('mpg123')
optdepends=('libnotify: for system notification')
license=('MIT')
arch=('any')
source=("https://pypi.python.org/packages/source/p/pyfm/pyfm-${pkgver}.tar.gz")
sha512sums=('b3eada7a8424568ab2d7f0459fc506c1863e07b048b370b5b5d52783e049d8ee72720964bab8ab241bc39ad4b66b1da0abfb007439090c35d0d07b45ef485c93')

prepare() {
    cp -a "${srcdir}/pyfm-${pkgver}"{,-python2}
}

package_pyfm() {
    depends+=('python-requests' 'python-urwid')
    makedepends=('python-setuptools')
    cd "pyfm-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pyfm() {
    depends+=('python2-requests' 'python2-urwid')
    makedepends=('python2-setuptools')
    cd "pyfm-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1
    mv "${pkgdir}/usr/bin/pyfm"{,2}
}
